// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

	// load control signal
	Or(a=load, b=inc, out=o1);
	Or(a=o1, b=reset, out=cload);

	Inc16(in=rout, out=incout);

	//  inc  load  reset |  xsel
	// ------------------+-------
	//   0     0     0   |  0  0  hold
	//   1     X     X   |  1  1  reset
	//   0     1     X   |  1  0  load
	//   0     0     1   |  0  1  inc

	Or(a=reset, b=load, out=xsel1);

	Not(in=reset, out=nreset);
	Not(in=load, out=nload);

	And(a=nload, b=nreset, out=a1);
	And(a=a1, b=inc, out=a2);
	Or(a=a2, b=reset, out=xsel0);

	// select one of the three inputs
	Mux4Way16(a=false, b=incout, c=in, d=false,
				sel[0]=xsel0, sel[1]=xsel1,
				out=xout);

	Register(in=xout, load=cload, out=rout, out=out);
}
